pragma Singleton
pragma ComponentBehavior: Bound
import QtQuick
import Quickshell
import Quickshell.Wayland // for layers
import qs.modules.common.functions

Singleton {
  id: root
  property QtObject m3colors
  property QtObject animation
  property QtObject animationCurves
  property QtObject colors
  property QtObject rounding
  property QtObject font
  property QtObject sizes
  property QtObject layers

  // Transparency. The quadratic functions were derived from analysis of hand-picked transparency values.
  ColorQuantizer {
    id: wallColorQuant
    property string wallpaperPath: Config.options.background.wallpaperPath
    property bool wallpaperIsVideo: wallpaperPath.endsWith(".mp4") || wallpaperPath.endsWith(".webm") || wallpaperPath.endsWith(".mkv") || wallpaperPath.endsWith(".avi") || wallpaperPath.endsWith(".mov")
    source: Qt.resolvedUrl(wallpaperIsVideo ? Config.options.background.thumbnailPath : Config.options.background.wallpaperPath)
    depth: 0 // 2^0 = 1 color
    rescaleSize: 10
  }
  property real wallpaperVibrancy: (wallColorQuant.colors[0]?.hslSaturation + wallColorQuant.colors[0]?.hslLightness) / 2
  property real autoBackgroundTransparency: { // y = 0.5768x^2 - 0.759x + 0.2896
    let x = wallpaperVibrancy;
    let y = 0.5768 * (x * x) - 0.759 * (x) + 0.2896;
    return Math.max(0, Math.min(0.22, y)) - 0.12 * (m3colors.darkmode ? 0 : 1);
  }
  property real autoContentTransparency: 0.9
  property real backgroundTransparency: Config?.options.appearance.transparency.enable ? Config?.options.appearance.transparency.automatic ? autoBackgroundTransparency : Config?.options.appearance.transparency.backgroundTransparency : 0
  property real contentTransparency: Config?.options.appearance.transparency.automatic ? autoContentTransparency : Config?.options.appearance.transparency.contentTransparency

  // Aether: calm optical glass material. Pearl background, graphite ink accents,
  // one blue focus/interaction accent, and a soft aurora wash reserved for hero surfaces.
  m3colors: QtObject {
    property bool darkmode: false
    property bool transparent: false
    property color m3background: "#141313"
    property color m3onBackground: "#e6e1e1"
    property color m3surface: "#141313"
    property color m3surfaceDim: "#141313"
    property color m3surfaceBright: "#3a3939"
    property color m3surfaceContainerLowest: "#0f0e0e"
    property color m3surfaceContainerLow: "#1c1b1c"
    property color m3surfaceContainer: "#201f20"
    property color m3surfaceContainerHigh: "#2b2a2a"
    property color m3surfaceContainerHighest: "#363435"
    property color m3onSurface: "#e6e1e1"
    property color m3surfaceVariant: "#49464a"
    property color m3onSurfaceVariant: "#cbc5ca"
    property color m3inverseSurface: "#e6e1e1"
    property color m3inverseOnSurface: "#313030"
    property color m3outline: "#948f94"
    property color m3outlineVariant: "#49464a"
    property color m3shadow: "#000000"
    property color m3scrim: "#000000"
    property color m3surfaceTint: "#cbc4cb"
    property color m3primary: "#cbc4cb"
    property color m3onPrimary: "#322f34"
    property color m3primaryContainer: "#2d2a2f"
    property color m3onPrimaryContainer: "#bcb6bc"
    property color m3inversePrimary: "#615d63"
    property color m3secondary: "#cac5c8"
    property color m3onSecondary: "#323032"
    property color m3secondaryContainer: "#4d4b4d"
    property color m3onSecondaryContainer: "#ece6e9"
    property color m3tertiary: "#d1c3c6"
    property color m3onTertiary: "#372e30"
    property color m3tertiaryContainer: "#31292b"
    property color m3onTertiaryContainer: "#c1b4b7"
    property color m3error: "#ffb4ab"
    property color m3onError: "#690005"
    property color m3errorContainer: "#93000a"
    property color m3onErrorContainer: "#ffdad6"
    property color m3primaryFixed: "#e7e0e7"
    property color m3primaryFixedDim: "#cbc4cb"
    property color m3onPrimaryFixed: "#1d1b1f"
    property color m3onPrimaryFixedVariant: "#49454b"
    property color m3secondaryFixed: "#e6e1e4"
    property color m3secondaryFixedDim: "#cac5c8"
    property color m3onSecondaryFixed: "#1d1b1d"
    property color m3onSecondaryFixedVariant: "#484648"
    property color m3tertiaryFixed: "#eddfe1"
    property color m3tertiaryFixedDim: "#d1c3c6"
    property color m3onTertiaryFixed: "#211a1c"
    property color m3onTertiaryFixedVariant: "#4e4447"
    property color m3success: "#B5CCBA"
    property color m3onSuccess: "#213528"
    property color m3successContainer: "#374B3E"
    property color m3onSuccessContainer: "#D1E9D6"
    property color term0: "#1d1d1f"
    property color term1: "#ff3b30"
    property color term2: "#ff9500"
    property color term3: "#ffcc00"
    property color term4: "#34c759"
    property color term5: "#0a84ff"
    property color term6: "#af52de"
    property color term7: "#f5f5f7"
    property color term8: "#86868b"
    property color term9: "#ff6961"
    property color term10: "#ffb340"
    property color term11: "#ffe066"
    property color term12: "#6ee89a"
    property color term13: "#5eb3ff"
    property color term14: "#cc8fef"
    property color term15: "#ffffff"
  }

  colors: QtObject {
    property color colSubtext: m3colors.m3outline
    // Layer 0
    property color colLayer0Base: ColorUtils.mix(m3colors.m3background, m3colors.m3primary, Config.options.appearance.extraBackgroundTint ? 0.99 : 1)
    property color colLayer0: ColorUtils.transparentize(colLayer0Base, root.backgroundTransparency)
    property color colOnLayer0: m3colors.m3onBackground
    property color colLayer0Hover: ColorUtils.transparentize(ColorUtils.mix(colLayer0, colOnLayer0, 0.9, root.contentTransparency))
    property color colLayer0Active: ColorUtils.transparentize(ColorUtils.mix(colLayer0, colOnLayer0, 0.8, root.contentTransparency))
    property color colLayer0Border: ColorUtils.mix(root.m3colors.m3outlineVariant, colLayer0, 0.4)
    // Layer 1
    property color colLayer1Base: m3colors.m3surfaceContainerLow
    property color colLayer1: ColorUtils.solveOverlayColor(colLayer0Base, colLayer1Base, 1 - root.backgroundTransparency)
    property color colOnLayer1: m3colors.m3onSurfaceVariant
    property color colOnLayer1Inactive: ColorUtils.mix(colOnLayer1, colLayer1, 0.45)
    property color colLayer1Hover: ColorUtils.transparentize(ColorUtils.mix(colLayer1, colOnLayer1, 0.92), root.contentTransparency)
    property color colLayer1Active: ColorUtils.transparentize(ColorUtils.mix(colLayer1, colOnLayer1, 0.85), root.contentTransparency)
    // Layer 2
    property color colLayer2Base: m3colors.m3surfaceContainer
    property color colLayer2: ColorUtils.solveOverlayColor(colLayer1Base, colLayer2Base, 1 - root.contentTransparency)
    property color colLayer2Hover: ColorUtils.solveOverlayColor(colLayer1Base, ColorUtils.mix(colLayer2Base, colOnLayer2, 0.90), 1 - root.contentTransparency)
    property color colLayer2Active: ColorUtils.solveOverlayColor(colLayer1Base, ColorUtils.mix(colLayer2Base, colOnLayer2, 0.80), 1 - root.contentTransparency)
    property color colLayer2Disabled: ColorUtils.solveOverlayColor(colLayer1Base, ColorUtils.mix(colLayer2Base, m3colors.m3background, 0.8), 1 - root.contentTransparency)
    property color colOnLayer2: m3colors.m3onSurface
    property color colOnLayer2Disabled: ColorUtils.mix(colOnLayer2, m3colors.m3background, 0.4)
    // Layer 3
    property color colLayer3Base: m3colors.m3surfaceContainerHigh
    property color colLayer3: ColorUtils.solveOverlayColor(colLayer2Base, colLayer3Base, 1 - root.contentTransparency)
    property color colLayer3Hover: ColorUtils.solveOverlayColor(colLayer2Base, ColorUtils.mix(colLayer3Base, colOnLayer3, 0.90), 1 - root.contentTransparency)
    property color colLayer3Active: ColorUtils.solveOverlayColor(colLayer2Base, ColorUtils.mix(colLayer3Base, colOnLayer3, 0.80), 1 - root.contentTransparency)
    property color colOnLayer3: m3colors.m3onSurface
    // Layer 4
    property color colLayer4Base: m3colors.m3surfaceContainerHighest
    property color colLayer4: ColorUtils.solveOverlayColor(colLayer3Base, colLayer4Base, 1 - root.contentTransparency)
    property color colLayer4Hover: ColorUtils.solveOverlayColor(colLayer3Base, ColorUtils.mix(colLayer4Base, colOnLayer4, 0.90), 1 - root.contentTransparency)
    property color colLayer4Active: ColorUtils.solveOverlayColor(colLayer3Base, ColorUtils.mix(colLayer4Base, colOnLayer4, 0.80), 1 - root.contentTransparency)
    property color colOnLayer4: m3colors.m3onSurface
    // Primary
    property color colPrimary: m3colors.m3primary
    property color colOnPrimary: m3colors.m3onPrimary
    property color colPrimaryHover: ColorUtils.mix(colors.colPrimary, colLayer1Hover, 0.87)
    property color colPrimaryActive: ColorUtils.mix(colors.colPrimary, colLayer1Active, 0.7)
    property color colPrimaryContainer: m3colors.m3primaryContainer
    property color colPrimaryContainerHover: ColorUtils.mix(colors.colPrimaryContainer, colors.colOnPrimaryContainer, 0.9)
    property color colPrimaryContainerActive: ColorUtils.mix(colors.colPrimaryContainer, colors.colOnPrimaryContainer, 0.8)
    property color colOnPrimaryContainer: m3colors.m3onPrimaryContainer
    // Secondary
    property color colSecondary: m3colors.m3secondary
    property color colSecondaryHover: ColorUtils.mix(m3colors.m3secondary, colLayer1Hover, 0.85)
    property color colSecondaryActive: ColorUtils.mix(m3colors.m3secondary, colLayer1Active, 0.4)
    property color colOnSecondary: m3colors.m3onSecondary
    property color colSecondaryContainer: m3colors.m3secondaryContainer
    property color colSecondaryContainerHover: ColorUtils.mix(m3colors.m3secondaryContainer, m3colors.m3onSecondaryContainer, 0.90)
    property color colSecondaryContainerActive: ColorUtils.mix(m3colors.m3secondaryContainer, m3colors.m3onSecondaryContainer, 0.54)
    property color colOnSecondaryContainer: m3colors.m3onSecondaryContainer
    // Tertiary
    property color colTertiary: m3colors.m3tertiary
    property color colTertiaryHover: ColorUtils.mix(m3colors.m3tertiary, colLayer1Hover, 0.85)
    property color colTertiaryActive: ColorUtils.mix(m3colors.m3tertiary, colLayer1Active, 0.4)
    property color colTertiaryContainer: m3colors.m3tertiaryContainer
    property color colTertiaryContainerHover: ColorUtils.mix(m3colors.m3tertiaryContainer, m3colors.m3onTertiaryContainer, 0.90)
    property color colTertiaryContainerActive: ColorUtils.mix(m3colors.m3tertiaryContainer, colLayer1Active, 0.54)
    property color colOnTertiary: m3colors.m3onTertiary
    property color colOnTertiaryContainer: m3colors.m3onTertiaryContainer
    // Surface
    property color colBackgroundSurfaceContainer: ColorUtils.transparentize(m3colors.m3surfaceContainer, root.backgroundTransparency)
    property color colSurfaceContainerLow: ColorUtils.solveOverlayColor(m3colors.m3background, m3colors.m3surfaceContainerLow, 1 - root.contentTransparency)
    property color colSurfaceContainer: ColorUtils.solveOverlayColor(m3colors.m3surfaceContainerLow, m3colors.m3surfaceContainer, 1 - root.contentTransparency)
    property color colSurfaceContainerHigh: ColorUtils.solveOverlayColor(m3colors.m3surfaceContainer, m3colors.m3surfaceContainerHigh, 1 - root.contentTransparency)
    property color colSurfaceContainerHighest: ColorUtils.solveOverlayColor(m3colors.m3surfaceContainerHigh, m3colors.m3surfaceContainerHighest, 1 - root.contentTransparency)
    property color colSurfaceContainerHighestHover: ColorUtils.mix(m3colors.m3surfaceContainerHighest, m3colors.m3onSurface, 0.95)
    property color colSurfaceContainerHighestActive: ColorUtils.mix(m3colors.m3surfaceContainerHighest, m3colors.m3onSurface, 0.85)
    property color colOnSurface: m3colors.m3onSurface
    property color colOnSurfaceVariant: m3colors.m3onSurfaceVariant
    // Misc
    property color colTooltip: m3colors.m3inverseSurface
    property color colOnTooltip: m3colors.m3inverseOnSurface
    property color colScrim: ColorUtils.transparentize(m3colors.m3scrim, 0.5)
    property color colShadow: ColorUtils.transparentize(m3colors.m3shadow, 0.82)
    property color colOutline: m3colors.m3outline
    property color colOutlineVariant: m3colors.m3outlineVariant
    property color colError: m3colors.m3error
    property color colErrorHover: ColorUtils.mix(m3colors.m3error, colLayer1Hover, 0.85)
    property color colErrorActive: ColorUtils.mix(m3colors.m3error, colLayer1Active, 0.7)
    property color colOnError: m3colors.m3onError
    property color colErrorContainer: m3colors.m3errorContainer
    property color colErrorContainerHover: ColorUtils.mix(m3colors.m3errorContainer, m3colors.m3onErrorContainer, 0.90)
    property color colErrorContainerActive: ColorUtils.mix(m3colors.m3errorContainer, m3colors.m3onErrorContainer, 0.70)
    property color colOnErrorContainer: m3colors.m3onErrorContainer
    // Aether glass material tokens
    property color colHairline: ColorUtils.transparentize(m3colors.m3onBackground, 0.92)
    property color colBevelLight: ColorUtils.transparentize(m3colors.m3surfaceContainerLowest, 0.1)
    property color colBevelSoft: ColorUtils.transparentize(m3colors.m3surfaceContainerLowest, 0.45)
    property color colGlass: ColorUtils.transparentize(m3colors.m3surfaceContainerLowest, 0.45)
    property color colGlassStrong: ColorUtils.transparentize(m3colors.m3surfaceContainerLowest, 0.25)
    property color colSurfaceRaised: ColorUtils.transparentize(m3colors.m3surfaceContainerLowest, 0.15)
    property color colFocusRing: ColorUtils.transparentize(m3colors.m3tertiary, 0.68)
    // Success
    property color colSuccess: m3colors.m3success
    property color colOnSuccess: m3colors.m3onSuccess
    property color colSuccessContainer: m3colors.m3successContainer
    property color colOnSuccessContainer: m3colors.m3onSuccessContainer
    // Menubar
    property color colMenubarBackground: (Config.options.bar?.background?.style === 0 ? (Appearance.colors.colLayer0) : Config.options.bar?.background?.style === 1 ? (Config.options.bar?.background?.color) : ColorUtils.transparentize(Appearance.colors.colLayer0, 0.8)) ?? Appearance.colors.colLayer0
    property color onMenubarBackground: Config.options.bar?.foreground?.style === 1 ? (Config.options.bar?.foreground?.color) : (Config.options.bar?.background?.enable ? (Config.options.bar?.background?.style === 0 ? (Appearance.m3colors.m3onBackground) : Config.options.bar?.background?.style === 1 ? ColorUtils.adaptColor(Config.options.bar?.background?.color) : Appearance.m3colors.m3onBackground) : Appearance.m3colors.m3onBackground)
  }

  rounding: QtObject {
    property int unsharpen: 2
    property int unsharpenmore: 6
    property int verysmall: 8
    property int small: 12
    property int normal: 16
    property int large: 24
    property int verylarge: 30
    property int full: 9999
    property int screenRounding: (Config.options?.hyprland?.decoration?.rounding + 4) ?? large
    property int windowRounding: 18
  }

  animationCurves: QtObject {
    readonly property list<real> expressiveFastSpatial: [0.42, 1.67, 0.21, 0.90, 1, 1] // Default, 350ms
    readonly property list<real> expressiveDefaultSpatial: [0.38, 1.21, 0.22, 1.00, 1, 1] // Default, 500ms
    readonly property list<real> expressiveSlowSpatial: [0.39, 1.29, 0.35, 0.98, 1, 1] // Default, 650ms
    readonly property list<real> expressiveEffects: [0.34, 0.80, 0.34, 1.00, 1, 1] // Default, 200ms
    readonly property list<real> emphasized: [0.05, 0, 2 / 15, 0.06, 1 / 6, 0.4, 5 / 24, 0.82, 0.25, 1, 1, 1]
    readonly property list<real> emphasizedFirstHalf: [0.05, 0, 2 / 15, 0.06, 1 / 6, 0.4, 5 / 24, 0.82]
    readonly property list<real> emphasizedLastHalf: [5 / 24, 0.82, 0.25, 1, 1, 1]
    readonly property list<real> emphasizedAccel: [0.3, 0, 0.8, 0.15, 1, 1]
    readonly property list<real> emphasizedDecel: [0.05, 0.7, 0.1, 1, 1, 1]
    readonly property list<real> standard: [0.2, 0, 0, 1, 1, 1]
    readonly property list<real> standardAccel: [0.3, 0, 1, 1, 1, 1]
    readonly property list<real> standardDecel: [0, 0, 0, 1, 1, 1]
    readonly property real expressiveFastSpatialDuration: 350
    readonly property real expressiveDefaultSpatialDuration: 500
    readonly property real expressiveSlowSpatialDuration: 650
    readonly property real expressiveEffectsDuration: 200
    // Aether glass motion: a calm ease-out for hover/state transitions,
    // and a light spring for chip/thumb bounces (reserved for momentum-driven,
    // "the user just touched this" interactions, not passive fades).
    readonly property list<real> aetherEaseOut: [0.22, 0.61, 0.36, 1, 1, 1]
    readonly property list<real> aetherEaseSpring: [0.5, 1.6, 0.4, 1, 1, 1]
    readonly property real aetherFastDuration: 140
    readonly property real aetherMedDuration: 220
  }

  animation: QtObject {
    property QtObject elementMove: QtObject {
      property int duration: animationCurves.expressiveDefaultSpatialDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.expressiveDefaultSpatial
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          duration: root.animation.elementMove.duration
          easing.type: root.animation.elementMove.type
          easing.bezierCurve: root.animation.elementMove.bezierCurve
        }
      }
    }

    property QtObject elementMoveSmall: QtObject {
      property int duration: animationCurves.expressiveFastSpatialDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.expressiveFastSpatial
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          duration: root.animation.elementMoveSmall.duration
          easing.type: root.animation.elementMoveSmall.type
          easing.bezierCurve: root.animation.elementMoveSmall.bezierCurve
        }
      }
    }

    property QtObject searchExpand: QtObject {
      property int duration: animationCurves.expressiveDefaultSpatialDuration
      property int type: Easing.OutExpo
      property list<real> bezierCurve: animationCurves.expressiveDefaultSpatial
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          duration: root.animation.elementMove.duration
          easing.type: root.animation.elementMove.type
          easing.bezierCurve: root.animation.elementMove.bezierCurve
        }
      }
    }

    property QtObject elementMoveEnter: QtObject {
      property int duration: 400
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.emphasizedDecel
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.elementMoveEnter.duration
          easing.type: root.animation.elementMoveEnter.type
          easing.bezierCurve: root.animation.elementMoveEnter.bezierCurve
        }
      }
    }

    property QtObject elementMoveExit: QtObject {
      property int duration: 200
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.emphasizedAccel
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.elementMoveExit.duration
          easing.type: root.animation.elementMoveExit.type
          easing.bezierCurve: root.animation.elementMoveExit.bezierCurve
        }
      }
    }

    property QtObject elementMoveFast: QtObject {
      property int duration: animationCurves.aetherMedDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.aetherEaseOut
      property int velocity: 850
      property Component colorAnimation: Component {
        ColorAnimation {
          duration: root.animation.elementMoveFast.duration
          easing.type: root.animation.elementMoveFast.type
          easing.bezierCurve: root.animation.elementMoveFast.bezierCurve
        }
      }
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.elementMoveFast.duration
          easing.type: root.animation.elementMoveFast.type
          easing.bezierCurve: root.animation.elementMoveFast.bezierCurve
        }
      }
    }

    property QtObject elementResize: QtObject {
      property int duration: 300
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.emphasized
      property int velocity: 650
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.elementResize.duration
          easing.type: root.animation.elementResize.type
          easing.bezierCurve: root.animation.elementResize.bezierCurve
        }
      }
    }

    property QtObject clickBounce: QtObject {
      property int duration: animationCurves.aetherMedDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.aetherEaseSpring
      property int velocity: 850
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.clickBounce.duration
          easing.type: root.animation.clickBounce.type
          easing.bezierCurve: root.animation.clickBounce.bezierCurve
        }
      }
    }

    property QtObject scroll: QtObject {
      property int duration: 200
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: root.animationCurves.standardDecel
    }

    property QtObject smooth: QtObject {
      property int duration: animationCurves.aetherMedDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.aetherEaseOut
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.smooth.duration
          easing.type: root.animation.smooth.type
          easing.bezierCurve: root.animation.smooth.bezierCurve
        }
      }
    }

    property QtObject hover: QtObject {
      property int duration: animationCurves.aetherFastDuration
      property int type: Easing.BezierSpline
      property list<real> bezierCurve: animationCurves.aetherEaseOut
      property Component numberAnimation: Component {
        NumberAnimation {
          alwaysRunToEnd: true
          duration: root.animation.hover.duration
          easing.type: root.animation.hover.type
          easing.bezierCurve: root.animation.hover.bezierCurve
        }
      }
    }

    property QtObject menuDecel: QtObject {
      property int duration: 350
      property int type: Easing.OutExpo
    }
  }

  // Typography scale factor from config
  property real fontSizeScale: Config.options?.appearance?.typography?.sizeScale ?? 1.0

  font: QtObject {
    property QtObject family: QtObject {
      property string main: Config.options.appearance.typography.main
      property string numbers: Config.options.appearance.typography.numbers
      property string title: Config.options.appearance.typography.title
      property string iconMaterial: "Material Symbols Rounded"
      property string iconNerd: Config.options.appearance.typography.iconNerd
      property string monospace: Config.options.appearance.typography.monospace
      property string reading: Config.options.appearance.typography.reading
      property string expressive: Config.options.appearance.typography.expressive
    }
    property QtObject variableAxes: QtObject {
      property var main: ({
          "wght": 450,
          "wdth": 100
        })
      property var numbers: ({
          "wght": 450
        })
      property var title: ({ // Slightly bold weight for title
          "wght": 550 // Weight (Lowered to compensate for increased grade)
        })
    }
    property QtObject pixelSize: QtObject {
      property int smallest: Math.round(10 * root.fontSizeScale)
      property int smaller: Math.round(12 * root.fontSizeScale)
      property int smallie: Math.round(13 * root.fontSizeScale)
      property int small: Math.round(15 * root.fontSizeScale)
      property int normal: Math.round(16 * root.fontSizeScale)
      property int large: Math.round(17 * root.fontSizeScale)
      property int larger: Math.round(19 * root.fontSizeScale)
      property int huge: Math.round(22 * root.fontSizeScale)
      property int hugeass: Math.round(23 * root.fontSizeScale)
      property int title: huge
    }
  }

  sizes: QtObject {
    property real baseBarHeight: 32
    property real barHeight: Config.options.bar.cornerStyle === 1 ? (baseBarHeight + root.sizes.hyprlandGapsOut * 2) : baseBarHeight
    property real elevationMargin: 10
    property real fabShadowRadius: 5
    property real fabHoveredShadowRadius: 7
    property real hyprlandGapsOut: 5
    property real mediaControlsWidth: 440
    property real mediaControlsHeight: 160
    property real notificationPopupWidth: 410
    property real osdWidth: 180
    property real searchWidth: 700
    property real sidebarWidth: 450
    property real sidebarWidthExtended: 750
    property real baseVerticalBarWidth: 46
    property real wallpaperSelectorWidth: 1200
    property real wallpaperSelectorHeight: 690
    property real wallpaperSelectorItemMargins: 8
    property real wallpaperSelectorItemPadding: 6
  }

  layers: QtObject {
    property real background: WlrLayer.Background
    property real lockscreen: WlrLayer.Overlay
    property real bar: WlrLayer.Bottom
    property real dock: WlrLayer.Bottom
  }
}
