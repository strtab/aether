import QtQuick
import QtQuick.Layouts
import Quickshell.Io
import Quickshell
import qs.modules.common.functions
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.models.hyprland

ContentPage {
  id: page
  forceWidth: true

  function goTo(term) {
    const t = term.toLowerCase().trim();

    function findTarget(rootItem) {
      for (let i = 0; i < rootItem.children.length; i++) {
        let child = rootItem.children[i];
        if (child.title && child.title.toLowerCase().includes(t)) {
          return child;
        }
      }
      for (let i = 0; i < rootItem.children.length; i++) {
        let found = findTarget(rootItem.children[i]);
        if (found)
          return found;
      }
      return null;
    }

    let target = findTarget(mainLayout);
    if (target) {
      let pos = target.mapToItem(mainLayout, 0, 0);
      page.contentY = Math.max(0, pos.y - 0);
    }
  }

  Component.onCompleted: {
    const h = Config.options.hyprland;
    HyprlandConfig.setMany({
      "decoration:rounding": h.decoration.rounding,
      "decoration:blur:enabled": h.decoration.blur.enabled ? 1 : 0,
      "decoration:blur:size": h.decoration.blur.size,
      "decoration:blur:passes": h.decoration.blur.passes,
      "decoration:active_opacity": h.decoration.activeOpacity,
      "decoration:inactive_opacity": h.decoration.inactiveOpacity,
      "general:border_size": h.general.borderSize,
      "general:gaps_in": h.general.gapsIn,
      "general:gaps_out": h.general.gapsOut,
      "general:layout": h.general.layout,
      "animations:enabled": h.animations.enable ? 1 : 0,
      "input:kb_layout": h.input.kbLayout,
      "input:numlock_by_default": h.input.numlock ? 1 : 0,
      "input:repeat_delay": h.input.repeatDelay,
      "input:repeat_rate": h.input.repeatRate,
      "input:follow_mouse": h.input.followMouse,
      "input:touchpad:natural_scroll": h.input.touchpad.naturalScroll ? 1 : 0,
      "input:touchpad:disable_while_typing": h.input.touchpad.disableWhileTyping ? 1 : 0,
      "input:touchpad:clickfinger_behavior": h.input.touchpad.clickfingerBehavior ? 1 : 0,
      "input:touchpad:scroll_factor": h.input.touchpad.scrollFactor
    });
  }

  MonitorConfigOption {
    id: monitorConfig
  }

  ColumnLayout {
    id: mainLayout
    Layout.fillWidth: true
    Layout.fillHeight: true
    spacing: 20

    // ── Displays ──────────────────────────────────────────────────────────
    ContentSection {
      icon: "monitor"
      title: Translation.tr("Displays")
      visible: monitorConfig.monitors.length > 0

      MonitorCanvas {
        id: monitorCanvas
        Layout.fillWidth: true
        monitorConfig: monitorConfig
      }

      ContentSubsection {
        title: (monitorConfig.monitors[monitorCanvas.selectedIndex]?.name ?? "") + " · " + (monitorConfig.monitors[monitorCanvas.selectedIndex]?.description ?? "")

        ConfigSwitch {
          buttonIcon: "tv_off"
          text: Translation.tr("Enabled")
          checked: !(monitorConfig.monitors[monitorCanvas.selectedIndex]?.disabled ?? false)
          onCheckedChanged: {
            monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
              disabled: !checked
            });
            monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
          }
        }

        ContentSubsection {
          title: Translation.tr("Resolution & Refresh Rate")
          StyledComboBoxSearch {
            buttonIcon: "aspect_ratio"
            model: (monitorConfig.monitors[monitorCanvas.selectedIndex]?.availableModes ?? []).map(mode => ({
                  display: mode,
                  value: mode
                }))
            textRole: "display"
            currentIndex: (monitorConfig.monitors[monitorCanvas.selectedIndex]?.availableModes ?? []).indexOf(monitorConfig.monitors[monitorCanvas.selectedIndex]?.currentMode ?? "")
            onActivated: {
              const mon = monitorConfig.monitors[monitorCanvas.selectedIndex];
              const mode = mon.availableModes[currentIndex];
              const parts = mode.match(/(\d+)x(\d+)@([\d.]+)Hz/);
              monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
                currentMode: mode,
                width: parseInt(parts[1]),
                height: parseInt(parts[2]),
                refreshRate: parseFloat(parts[3])
              });
              monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
            }
          }
        }

        ContentSubsection {
          title: Translation.tr("Orientation")
          ConfigSelectionArray {
            currentValue: monitorConfig.monitors[monitorCanvas.selectedIndex]?.transform ?? 0
            onSelected: newValue => {
              monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
                transform: newValue
              });
              monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
            }
            options: [
              {
                displayName: Translation.tr("Normal"),
                icon: "screen_rotation_alt",
                value: 0
              },
              {
                displayName: "90°",
                icon: "rotate_90_degrees_cw",
                value: 1
              },
              {
                displayName: "180°",
                icon: "screen_rotation",
                value: 2
              },
              {
                displayName: "270°",
                icon: "rotate_90_degrees_ccw",
                value: 3
              },
            ]
          }
        }

        ConfigSpinBox {
          icon: "zoom_in"
          text: Translation.tr("Scale")
          value: Math.round((monitorConfig.monitors[monitorCanvas.selectedIndex]?.scale ?? 1.0) * 100)
          from: 50
          to: 300
          stepSize: 20
          onValueChanged: {
            monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
              scale: value / 100.0
            });
            monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
          }
        }

        ConfigSpinBox {
          icon: "swap_horiz"
          text: Translation.tr("Position X")
          value: monitorConfig.monitors[monitorCanvas.selectedIndex]?.x ?? 0
          from: 0
          to: 7680
          stepSize: 1
          onValueChanged: {
            monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
              x: value
            });
            monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
          }
        }

        ConfigSpinBox {
          icon: "swap_vert"
          text: Translation.tr("Position Y")
          value: monitorConfig.monitors[monitorCanvas.selectedIndex]?.y ?? 0
          from: 0
          to: 4320
          stepSize: 1
          onValueChanged: {
            monitorConfig.updateMonitor(monitorCanvas.selectedIndex, {
              y: value
            });
            monitorConfig.applyAndSave(monitorCanvas.selectedIndex);
          }
        }
      }
    }

    // ── Layout ────────────────────────────────────────────────────────────
    ContentSection {
      icon: "auto_awesome_mosaic"
      title: Translation.tr("Layout")

      ContentSubsection {
        title: Translation.tr("Tiling Layout")
        ConfigSelectionArray {
          currentValue: Config.options.hyprland.general.layout
          onSelected: newValue => {
            Config.options.hyprland.general.layout = newValue;
            HyprlandConfig.set("general:layout", newValue);
          }
          options: [
            {
              displayName: Translation.tr("Dwindle"),
              icon: "browse",
              value: "dwindle"
            },
            {
              displayName: Translation.tr("Master"),
              icon: "auto_awesome_mosaic",
              value: "master"
            },
            {
              displayName: Translation.tr("Scrolling"),
              icon: "view_carousel",
              value: "scrolling"
            },
          ]
        }
      }
    }

    // ── Input ─────────────────────────────────────────────────────────────
    ContentSection {
      icon: "trackpad_input"
      title: Translation.tr("Input")

      ContentSubsection {
        title: Translation.tr("Keyboard")

        Input {
          id: kbLayoutTextArea
          Layout.fillWidth: true
          placeholderText: Translation.tr("Keyboard layout (e.g., us, es, latam)")
          wrapMode: TextEdit.NoWrap
          Component.onCompleted: text = Config.options.hyprland.input.kbLayout
          Timer {
            id: kbLayoutDebounceTimer
            interval: 1000
            running: false
            onTriggered: {
              Config.options.hyprland.input.kbLayout = kbLayoutTextArea.text;
              HyprlandConfig.set("input:kb_layout", kbLayoutTextArea.text);
            }
          }
          onTextChanged: kbLayoutDebounceTimer.restart()
        }

        ConfigSwitch {
          buttonIcon: "numbers"
          text: Translation.tr("Numlock by default")
          checked: Config.options.hyprland.input.numlock
          onCheckedChanged: {
            Config.options.hyprland.input.numlock = checked;
            HyprlandConfig.set("input:numlock_by_default", checked ? 1 : 0);
          }
        }

        ConfigSpinBox {
          icon: "keyboard_return"
          text: Translation.tr("Repeat delay (ms)")
          value: Config.options.hyprland.input.repeatDelay
          from: 100
          to: 1000
          stepSize: 10
          onValueChanged: {
            Config.options.hyprland.input.repeatDelay = value;
            HyprlandConfig.set("input:repeat_delay", value);
          }
        }

        ConfigSpinBox {
          icon: "speed"
          text: Translation.tr("Repeat rate")
          value: Config.options.hyprland.input.repeatRate
          from: 10
          to: 100
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.input.repeatRate = value;
            HyprlandConfig.set("input:repeat_rate", value);
          }
        }

        ConfigSelectionArray {
          currentValue: Config.options.hyprland.input.followMouse
          onSelected: newValue => {
            Config.options.hyprland.input.followMouse = newValue;
            HyprlandConfig.set("input:follow_mouse", newValue);
          }
          options: [
            {
              displayName: Translation.tr("Disabled"),
              icon: "mouse",
              value: 0
            },
            {
              displayName: Translation.tr("Full"),
              icon: "open_with",
              value: 1
            },
            {
              displayName: Translation.tr("Loose"),
              icon: "drag_pan",
              value: 2
            },
            {
              displayName: Translation.tr("Explicit"),
              icon: "ads_click",
              value: 3
            },
          ]
        }
      }

      ContentSubsection {
        title: Translation.tr("Touchpad")

        ConfigSwitch {
          buttonIcon: "swap_vert"
          text: Translation.tr("Natural scroll")
          checked: Config.options.hyprland.input.touchpad.naturalScroll
          onCheckedChanged: {
            Config.options.hyprland.input.touchpad.naturalScroll = checked;
            HyprlandConfig.set("input:touchpad:natural_scroll", checked ? 1 : 0);
          }
        }

        ConfigSwitch {
          buttonIcon: "keyboard_hide"
          text: Translation.tr("Disable while typing")
          checked: Config.options.hyprland.input.touchpad.disableWhileTyping
          onCheckedChanged: {
            Config.options.hyprland.input.touchpad.disableWhileTyping = checked;
            HyprlandConfig.set("input:touchpad:disable_while_typing", checked ? 1 : 0);
          }
        }

        ConfigSwitch {
          buttonIcon: "touch_app"
          text: Translation.tr("Clickfinger behavior")
          checked: Config.options.hyprland.input.touchpad.clickfingerBehavior
          onCheckedChanged: {
            Config.options.hyprland.input.touchpad.clickfingerBehavior = checked;
            HyprlandConfig.set("input:touchpad:clickfinger_behavior", checked ? 1 : 0);
          }
        }

        ConfigSpinBox {
          icon: "swipe"
          text: Translation.tr("Scroll factor")
          value: Math.round(Config.options.hyprland.input.touchpad.scrollFactor * 10)
          from: 1
          to: 30
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.input.touchpad.scrollFactor = value / 10.0;
            HyprlandConfig.set("input:touchpad:scroll_factor", value / 10.0);
          }
        }
      }
    }

    // ── Visual & Aesthetics ───────────────────────────────────────────────
    ContentSection {
      icon: "deblur"
      title: Translation.tr("Visual & Aesthetics")

      ConfigSpinBox {
        icon: "rounded_corner"
        text: Translation.tr("Window Rounding")
        value: Config.options.hyprland.decoration.rounding
        from: 0
        to: 30
        stepSize: 1
        onValueChanged: {
          Config.options.hyprland.decoration.rounding = value;
          HyprlandConfig.set("decoration:rounding", value);
        }
      }

      ConfigSpinBox {
        icon: "border_outer"
        text: Translation.tr("Border Size")
        value: Config.options.hyprland.general.borderSize
        from: 0
        to: 10
        stepSize: 1
        onValueChanged: {
          Config.options.hyprland.general.borderSize = value;
          HyprlandConfig.set("general:border_size", value);
        }
      }

      ContentSubsection {
        title: Translation.tr("Blur")
        ConfigSwitch {
          buttonIcon: "blur_on"
          text: Translation.tr("Blur")
          checked: Config.options.hyprland.decoration.blur.enabled
          onCheckedChanged: {
            Config.options.hyprland.decoration.blur.enabled = checked;
            HyprlandConfig.set("decoration:blur:enabled", checked ? 1 : 0);
          }
        }

        ConfigSpinBox {
          icon: "blur_circular"
          text: Translation.tr("Blur Size")
          value: Config.options.hyprland.decoration.blur.size
          from: 1
          to: 20
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.decoration.blur.size = value;
            HyprlandConfig.set("decoration:blur:size", value);
          }
        }

        ConfigSpinBox {
          icon: "layers"
          text: Translation.tr("Blur Passes")
          value: Config.options.hyprland.decoration.blur.passes
          from: 1
          to: 6
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.decoration.blur.passes = value;
            HyprlandConfig.set("decoration:blur:passes", value);
          }
        }
      }

      ContentSubsection {
        title: Translation.tr("Gaps")

        ConfigSpinBox {
          icon: "margin"
          text: Translation.tr("Gaps In")
          value: Config.options.hyprland.general.gapsIn
          from: 0
          to: 40
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.general.gapsIn = value;
            HyprlandConfig.set("general:gaps_in", value);
          }
        }

        ConfigSpinBox {
          icon: "open_in_full"
          text: Translation.tr("Gaps Out")
          value: Config.options.hyprland.general.gapsOut
          from: 0
          to: 60
          stepSize: 1
          onValueChanged: {
            Config.options.hyprland.general.gapsOut = value;
            HyprlandConfig.set("general:gaps_out", value);
          }
        }
      }

      ContentSubsection {
        title: Translation.tr("Opacity")

        ConfigSpinBox {
          icon: "opacity"
          text: Translation.tr("Active Opacity")
          value: Math.round(Config.options.hyprland.decoration.activeOpacity * 100)
          from: 10
          to: 100
          stepSize: 5
          onValueChanged: {
            Config.options.hyprland.decoration.activeOpacity = value / 100.0;
            HyprlandConfig.set("decoration:active_opacity", value / 100.0);
          }
        }

        ConfigSpinBox {
          icon: "opacity"
          text: Translation.tr("Inactive Opacity")
          value: Math.round(Config.options.hyprland.decoration.inactiveOpacity * 100)
          from: 10
          to: 100
          stepSize: 5
          onValueChanged: {
            Config.options.hyprland.decoration.inactiveOpacity = value / 100.0;
            HyprlandConfig.set("decoration:inactive_opacity", value / 100.0);
          }
        }
      }
    }

    // // ── Autostart Apps ────────────────────────────────────────────────────
    ContentSection {
      icon: "app_registration"
      title: Translation.tr("Autostart Apps")
      Layout.fillWidth: true

      AutostartApps {}
    }

    // ── Animations ────────────────────────────────────────────────────────
    ContentSection {
      icon: "animation"
      title: Translation.tr("Animations")

      ConfigSwitch {
        buttonIcon: "check"
        text: Translation.tr("Enable Animations")
        checked: Config.options.hyprland.animations.enable
        onCheckedChanged: {
          Config.options.hyprland.animations.enable = checked;
          HyprlandConfig.set("animations:enabled", checked ? 1 : 0);
        }
      }

      ContentSubsection {
        title: Translation.tr("Animation Preset")

        ConfigSelectionArray {
          currentValue: Config.options.hyprland.animations.animation
          onSelected: newValue => {
            Config.options.hyprland.animations.animation = newValue;
            saveAnimProc.command = ["python3", HyprlandConfig.configuratorScriptPath, "--anim-preset", newValue];
            saveAnimProc.running = true;
          }
          options: [
            {
              displayName: Translation.tr("Normal"),
              icon: "animation",
              value: "normal"
            },
            {
              displayName: Translation.tr("Reduced"),
              icon: "arrow_cool_down",
              value: "reduced"
            },
            {
              displayName: Translation.tr("Elastic"),
              icon: "move_selection_right",
              value: "fast"
            },
            {
              displayName: Translation.tr("Niri Like"),
              icon: "mobiledata_arrows",
              value: "niri"
            },
          ]
        }
      }

      Process {
        id: saveAnimProc
        onRunningChanged: if (!running)
          reloadAnimProc.running = true
      }
      Process {
        id: reloadAnimProc
        command: ["hyprctl", "reload"]
      }
    }
  }
}
